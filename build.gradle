plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version'7.1.2'
}

group = 'ruben_artz.main'
version = '5.7.19'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name 'minecraft-repo'
        url = 'https://libraries.minecraft.net/'
    }

    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '23.0.0'

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])

    implementation 'net.kyori:adventure-text-minimessage:4.12.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.1.2'
    implementation 'com.github.cryptomorin:XSeries:8.8.0'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'com.zaxxer:HikariCP:4.0.3'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

shadowJar {
    archiveFileName.set("Deluxe Mentions.jar")

    relocate("net.kyori", "ruben_artz.main.spigot.libs.kyori")
    relocate("com.cryptomorin.xseries", "ruben_artz.main.spigot.libs.xseries")
    relocate("com.zaxxer.hikari", "ruben_artz.main.spigot.libs.hikari")
    relocate("org.bstats", "ruben_artz.main.spigot.libs.bstats")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
