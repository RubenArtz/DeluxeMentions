plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'io.github.slimjar' version '1.3.0'
}

group = 'ruben_artz.main'
version = '6.2.21'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name 'minecraft-repo'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        url = 'https://repo.alessiodp.com/releases/'
    }

    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.30'
    compileOnly "org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT"
    /*
    Keep up to date
    Url: https://www.spigotmc.org/resources/6245/
     */
    compileOnly 'me.clip:placeholderapi:2.11.4'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly 'org.jetbrains:annotations:23.0.0'

    implementation 'io.github.slimjar:slimjar:1.2.7'
    implementation 'net.kyori:adventure-text-minimessage:4.17.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.3'
    implementation 'org.bstats:bstats-bukkit:3.0.0'

    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])

    /*
    Keep up to date
    Url: https://github.com/CryptoMorin/XSeries/releases
     */
    slim 'com.github.cryptomorin:XSeries:11.2.0'
    slim 'org.slf4j:slf4j-simple:1.7.36'
    slim 'com.zaxxer:HikariCP:4.0.3'
    slim 'com.h2database:h2:2.1.214'
    slim 'com.github.Anon8281:UniversalScheduler:0.1.6'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

shadowJar {
    archiveFileName.set("Deluxe Mentions.jar")

    relocate("io.github.slimjar", "ruben_artz.main.spigot.libs.slimjar")
    relocate("net.kyori", "ruben_artz.main.spigot.libs.kyori")
    relocate("org.bstats", "ruben_artz.main.spigot.libs.bstats")
}

slimJar {
    relocate("com.cryptomorin.xseries", "ruben_artz.main.spigot.libs.xseries")
    relocate("com.zaxxer.hikari", "ruben_artz.main.spigot.libs.hikari")
    relocate("org.h2", "ruben_artz.main.spigot.libs.h2")
    relocate("com.github.Anon8281.universalScheduler", "ruben_artz.main.spigot.libs.universalScheduler")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
